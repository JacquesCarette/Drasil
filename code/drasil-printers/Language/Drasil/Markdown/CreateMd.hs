module Language.Drasil.Markdown.CreateMd (
    makeMd, introInfo, verInfo, unsupOS, extLibSec, regularSec, instDoc) 
    where

import Prelude hiding ((<>))
import Text.PrettyPrint.HughesPJ (Doc, empty, isEmpty, vcat, text, (<+>),
    (<>), comma, punctuate, doubleQuotes, hsep)

type Seperator = Doc

-- Combines a list of sentences into a final Doc, also apeends end note
makeMd :: [Doc] -> Doc
makeMd lst = (vcat . punctuate secSep . filtEmp) lst <> contSep <>
    doubleSep <> endNote 

-- Example title and purpose section
introInfo :: String -> [String] -> Doc
introInfo name auths = introSec (text name) (listToDoc auths)

-- Instruction section, contains 3 paragraphs, Running, Building and Config Files.
-- The Config file section is only displayed if there are configuration files.
instDoc :: [String] -> Doc
instDoc cfp = regularSec (text "Making Examples") 
    (runInstDoc <> doubleSep <> makeInstDoc) <> configSec cfp 

commandLine :: Doc
commandLine = text $ "In your terminal command line, enter the same directory as this " ++
    "README file. Then enter the following line"

runInstDoc :: Doc
runInstDoc = text "How to Run the Program:" <> contSep <>
    commandLine <> contSep <> text "`make run RUNARGS=input.txt`"

makeInstDoc :: Doc
makeInstDoc = text "How to Build the Program:" <> contSep <> commandLine <> contSep <>
    text "`make build`"

configSec :: [String] -> Doc
configSec [] = empty
configSec cfp = doubleSep <> regularSec (text "Configuration Files") (listToDoc cfp <> contSep <>
    text "(Configuration files are files that must be in same directory as the executable in order to run or build successfully)")

-- Language version section
verInfo :: String -> String -> Doc
verInfo pl plv = regularSec (text "Versioning") (text $ pl ++ " Version: " ++ plv)

-- Invalid Operating Systems section, does not display unless atleast 1 invalid OS
unsupOS :: Maybe String -> Doc
unsupOS = maybe empty (\uns-> regularSec (text "Unsupported Operating Systems")
    (text $ "- " ++ uns))

-- External Libraries section. The inputs are a list of name and version pairs
-- and a list of the corresponding version numbers, these are first combined into a 
-- list of triplets, and then each printed on a new line
extLibSec:: [(String, String)] -> [String]-> Doc
extLibSec libns libfps = 
    let libs = addListToTuple libns libfps
        formattedLibs = (hsep . punctuate contSep . filtEmp . 
            map libStatment) libs
    in if isEmpty formattedLibs then empty else 
            regularSec (text "External Libraries") formattedLibs

libStatment :: (String, String, String) -> Doc
libStatment ("","", _) = empty
libStatment (nam,vers, fp) = (doubleQuotes . text) nam <+> text "version" 
    <+> text vers <+> if fp == "" then empty else
    text ", local file path to the library" <+> (doubleQuotes . text) fp

addListToTuple :: [(String,String)] -> [String] -> [(String, String, String)]
addListToTuple [] [] = []
addListToTuple ((n,v):_) [] = [(n,v,"")]
addListToTuple ((n,v):xtup) (l:xlst) = (n,v,l):addListToTuple xtup xlst
addListToTuple _ _ = []

-- End section
license :: Doc
license = empty -- TODO add license and logo
endNote :: Doc
endNote = license <> text "*This README is a software artifact generated by Drasil."

-- Section seperators
secSep, contSep, doubleSep :: Seperator
secSep = text "\n------------------------------------------------------------"
contSep = text "\n"
doubleSep = text "\n\n"

-- Functions to construct section from header and message
-- FIXME as explained in #2224 we still need to add in the purpose section, 
-- this could be done by adding a third parameter to introSec
introSec ::  Doc -> Doc -> Doc
introSec hd ms1 = text "#" <+> hd <+> contSep <> text "> Authors: " <+> ms1 

regularSec :: Doc -> Doc -> Doc
regularSec hd ms = text "**" <> hd <> text ":**" <+> contSep <+> ms

-- Helper for makeMd and extLibSec
filtEmp :: [Doc] -> [Doc]
filtEmp = filter (not . isEmpty) 

-- Helper for authors and configuration files
listToDoc :: [String] -> Doc
listToDoc = hsep . punctuate comma . map text
